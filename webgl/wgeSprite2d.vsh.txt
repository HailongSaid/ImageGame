/*
 * wgeSprite2d.vsh
 *
 *  Created on: 2014-8-6
 *      Author: Wang Yang
 *        Blog: http://blog.wysaid.org
 */
 
//Range: [-1, 1]
attribute vec2 vPosition; 
varying vec2 textureCoordinate;
uniform mat4 m4Projection;
uniform vec2 v2HalfTexSize;

uniform float rotation;
uniform vec2 v2Scaling;
uniform vec2 v2Translation;
uniform vec2 v2Hotspot;
uniform vec2 canvasflip;
uniform vec2 spriteflip;
uniform float zIndex;

mat3 mat3ZRotation(float rad)
{
    float cosRad = cos(rad);
    float sinRad = sin(rad);
    return mat3(cosRad, sinRad, 0.0, -sinRad, cosRad, 0.0, 0.0, 0.0, 1.0);
}

void main()
{
	textureCoordinate = (vPosition.xy * spriteflip + 1.0) / 2.0;
	vec3 pos = mat3ZRotation(rotation) * vec3((vPosition - v2Hotspot) * v2HalfTexSize, zIndex);
	pos.xy = (pos.xy + v2Hotspot * v2HalfTexSize);
	pos.xy *= v2Scaling;
	pos.xy += v2Translation - v2Scaling * v2HalfTexSize * v2Hotspot;
	gl_Position = m4Projection * vec4(pos, 1.0);
	gl_Position.xy *= canvasflip;
}